# Maintainer: Alberto Fanjul Alonso <albfan@gnome.org>

_realname=elementary-icon-theme
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=5.3.1
pkgrel=2
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64')
pkgdesc="git repository viewer for GTK+/GNOME (mingw-w64)"
depends=( "${MINGW_PACKAGE_PREFIX}-gtk3")
makedepends=("${MINGW_PACKAGE_PREFIX}-meson"
             "${MINGW_PACKAGE_PREFIX}-python-pillow"
             "${MINGW_PACKAGE_PREFIX}-ninja"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config")
options=('strip')
license=("GPL3")
url="https://github.com/elementary/icons"
source=(${_realname}-${pkgver}::git+https://github.com/elementary/icons.git#tag=${pkgver}
        Hard-copy-symlinks.patch
        Replace-xcursorgen-with-anicursorgen.patch)
sha256sums=('SKIP'
            'd1911dc8e60cb30a9da3e608db2b8e5e02cdb26777846c3f82ee877e172bae74'
            'bd159ebf3a515acd31741079303e133b440354e1f131c1df84e4564a1d6d8391')

prepare() {
  cd "$srcdir/${_realname}-${pkgver}"

  patch -N -p1 -i $srcdir/Hard-copy-symlinks.patch
  patch -N -p1 -i $srcdir/Replace-xcursorgen-with-anicursorgen.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --buildtype plain \
    -Dvolume_icons=false \
    ../${_realname}-${pkgver}

  ninja
}

package() {
  cd "$srcdir/build-${MINGW_CHOST}"
  DESTDIR="${pkgdir}" ninja install

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/COPYING" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/COPYING"
}
